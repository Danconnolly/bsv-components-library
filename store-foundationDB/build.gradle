plugins {
    id 'org.springframework.boot' version '2.1.9.RELEASE'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id 'java'
    id 'groovy'
    id 'java-library'
    id 'maven-publish'
}

/****************************************
 * Local Configuration
 ****************************************/
jclGroup = "$jclGroup"
jclVersion = "$jclVersion"
jclArtifactName = "$jclArtifactName"

group = "$rootProject.jclGroup"
version = "$rootProject.jclVersion"
sourceCompatibility = "11"
targetCompatibility = "11"

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url = "http://aire.sleepingsalmon.com:2095/repository/maven-releases/"
        allowInsecureProtocol = true
        credentials {
            username = "$nexusUser"
            password = "$nexusPassword"
        }
    }
}

dependencies {
    // Lombok
    //annotationProcessor 'org.projectlombok:lombok'
    //compileOnly 'org.projectlombok:lombok'

    // Compile Dependencies:
    implementation "io.bitcoinj:bitcoinj-base:2.0-SNAPSHOT"
    implementation(project(":tools"))
    implementation(project(":store"))
    implementation(project(":store-keyValue"))
    implementation 'org.foundationdb:fdb-java:6.2.22'
    implementation 'org.slf4j:slf4j-api:1.7.26'

    // Testing
    
    // NOTE:
    // WE are setting up a dependency on the TESTS from the JCL-Store. This might not be a good practice, but in this
    // case its OK, since the JCL-Module defines the interfaces and the Tests that those interfaces must meet. So the
    // Tests are oly defined ONCe in the JCL-Store module, and in this project we just extend them and add the 
    // concrete logic to create instances of those interfaces:

    testImplementation project(':store').sourceSets.test.output
    
    
    testImplementation 'org.codehaus.groovy:groovy'
    testImplementation "org.spockframework:spock-core:1.2-groovy-2.5"
    testImplementation "ch.qos.logback:logback-classic:1.2.3"
    testImplementation 'org.projectlombok:lombok'

    // TestContainer, to start automatically a FoundationDB running in a Docker container...
    // NOTE: Not used at the moment, since starting and stopping the container is NOT enough: We also need to
    // INITIALIZE the DataBase, meaning we need to run some Shell Scripts. So instead of using TestContainers, we
    // are just executing some Shell Scripts Before and After the Tests (check DockerTestUtils class).

    testImplementation 'org.testcontainers:spock:1.15.1'

}

bootJar {
    enabled = false
}

jar {
    enabled = true
}

/****************************************
 * Publish the JAR
 ****************************************/
publishing {
    publications {
        mavenJava(MavenPublication) {

            artifactId = "$jclArtifactName"
            groupId = "$jclGroup"
            version = "$jclVersion"

            from components.java

            pom {
                name = "$jclArtifactName"
                description = 'Java implementation of Bitcoin components'
                url = 'https://bitbucket.org/nchteamnch/java-component-library'

                licenses {
                    license {
                        name = ''
                        url = ''
                    }
                }
                developers {
                    developer {
                        name = 'Ivan Fernandez'
                        email = 'i.fernandez@nchain.com'
                    }
                    developer {
                        name = 'Michael Fletcher'
                        email = 'm.fletcher@nchain.com'
                    }
                }
                scm {
                    url = 'https://bitbucket.org/nchteamnch/java-component-library'
                }
            }
        }
    }

    repositories {
        maven {
            url = "http://aire.sleepingsalmon.com:2095/repository/maven-releases/"
            allowInsecureProtocol = true
            credentials {
                username = "$nexusUser"
                password = "$nexusPassword"
            }
        }
    }
}