plugins {
    id 'java'
    id 'maven-publish'
}

/****************************************
 * Local Configuration
 ****************************************/
jclGroup = "$jclGroup"
jclVersion = "$jclVersion"

group = "$rootProject.jclGroup"
version = "$rootProject.jclVersion"

sourceCompatibility = "11"
targetCompatibility = "11"

/****************************************
 * Dependencies on specific libraries
 ****************************************/
ext['logback.version'] = '1.2.8'
ext['log4j2.version'] = '2.16.0' // Just in Case (log4J vulnerability patch)

configurations {
    childJars
}

repositories {
    mavenCentral()
    jcenter()
    mavenLocal()
    maven {
        url = "$nexusReleasesUrl"
        allowInsecureProtocol = true
        credentials {
            username = "$nexusUser"
            password = "$nexusPassword"
        }
    }
}

/****************************************
 * Build the JAR - Include all sub project classes and dependencies
 ****************************************/
jar {
    from {
        subprojects.collect { it.sourceSets.main.output }
    }
}

/***********************************************************
 * Publish the FAT JAR (including ALL JCL in one single JAR)
 ***********************************************************/
publishing {
    publications {
        mavenJava(MavenPublication) {

            artifactId = "$jclArtifactName"
            groupId = "$jclGroup"
            version = "$jclVersion"

            from components.java

            pom {
                name = "$jclArtifactName"
                description = 'Java implementation of Bitcoin components'
                url = 'https://bitbucket.org/nchteamnch/java-component-library'

                licenses {
                    license {
                        name = ''
                        url = ''
                    }
                }
                developers {
                    developer {
                        name = 'Ivan Fernandez'
                        email = 'i.fernandez@nchain.com'
                    }
                    developer {
                        name = 'Michael Fletcher'
                        email = 'm.fletcher@nchain.com'
                    }
                }
                scm {
                    url = 'https://bitbucket.org/nchteamnch/java-component-library'
                }
            }
        }
    }

    repositories {
        maven {
            allowInsecureProtocol = true
            credentials {
                username = "$nexusUser"
                password = "$nexusPassword"
            }
            if(project.version.endsWith('-SNAPSHOT')) {
                url "$nexusSnapshotsUrl"
            } else {
                url "$nexusReleasesUrl"
            }
        }
    }
}