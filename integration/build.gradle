plugins {
    id 'org.springframework.boot' version '3.1.0'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'groovy'
    id 'java-library'
    id 'maven-publish'
}

/****************************************
 * Local Configuration
 ****************************************/
jclGroup = "$jclGroup"
jclVersion = "$jclVersion"
jclArtifactName = "jcl-integration"

group = "$rootProject.jclGroup"
version = "$rootProject.jclVersion"
sourceCompatibility = "11"
targetCompatibility = "11"

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    // Compile Dependencies:
    implementation "io.bitcoinsv.bitcoinjsv:bitcoinj-base:$bitcoinJVersion"
    implementation(project(":tools"))
    implementation(project(":net"))
    implementation(project(":store"))
    implementation(project(":store-keyValue"))
    implementation(project(":store-levelDB"))

    implementation 'org.slf4j:slf4j-api:1.7.26'
    implementation "ch.qos.logback:logback-classic"

    // Testing
    testImplementation(project(":tools"))
    testImplementation 'org.codehaus.groovy:groovy'
    testImplementation "org.spockframework:spock-core:1.2-groovy-2.5"
    testImplementation "ch.qos.logback:logback-classic"
    testImplementation 'com.madgag.spongycastle:core:1.51.0.0'
    testImplementation 'org.iq80.leveldb:leveldb:0.12'
}

bootJar {
    enabled = false
}

jar {
    enabled = true
    archiveClassifier = '' // avoid "-plain.jar" suffix in JAR file
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    if (JavaVersion.current().isJava11Compatible()) {
        options.addBooleanOption('Xdoclint:none', true)
    }
}

/****************************************
 * Packaging a FAT Jar with jCLServer
 ****************************************/
task packageJclServer(type: Jar) {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    bootJar {
        manifest {
            attributes 'Start-Class': 'io.bitcoinsv.jcl.integration.JCLServer'
        }
    }
    archiveFileName = 'jclServer.jar'
    from(configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude 'META-INF/*.SF', 'META-INF/*.RSA'
    }
    with jar
}

/****************************************
 * Packaging a FAT Jar with ChainDownload
 ****************************************/
task packageJclDownload(type: Jar) {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    bootJar {
        manifest {
            attributes 'Start-Class': 'io.bitcoinsv.jcl.integration.ChainDownload'
        }
    }
    archiveFileName = 'jclDownload.jar'
    from(configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude 'META-INF/*.SF', 'META-INF/*.RSA'
    }
    with jar
}

/****************************************
 * Publish the JAR
 ****************************************/
publishing {
    publications {
        mavenJava(MavenPublication) {

            artifactId = "$jclArtifactName"
            groupId = "$jclGroup"
            version = "$jclVersion"

            from components.java

            pom {
                name = "$jclArtifactName"
                description = 'Java implementation of Bitcoin components'
                url = 'https://github.com/bitcoin-sv/bsv-components-library'

                licenses {
                    license {
                        name = 'Open BSV License'
                        url = 'https://github.com/bitcoin-sv/bsv-components-library/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        organization = 'nChain'
                        organizationUrl = 'https://nchain.com'
                        name = 'i.fernandez'
                    }
                }
                scm {
                    url = 'https://github.com/bitcoin-sv/bsv-components-library'
                }
            }
        }
    }

    repositories {
        mavenLocal()
    }
}
