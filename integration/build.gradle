plugins {
    id 'org.springframework.boot' version '2.1.9.RELEASE'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id 'java'
    id 'groovy'
    id 'java-library'
    id 'maven-publish'
}

/****************************************
 * Local Configuration
 ****************************************/
jclGroup = "$jclGroup"
jclVersion = "$jclVersion"
jclArtifactName = "$jclArtifactName"

group = "$rootProject.jclGroup"
version = "$rootProject.jclVersion"
sourceCompatibility = "11"
targetCompatibility = "11"

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url = "$nexusReleasesUrl"
        allowInsecureProtocol = true
        credentials {
            username = "$nexusUser"
            password = "$nexusPassword"
        }
    }
}

dependencies {
    // Compile Dependencies:
    implementation "io.bitcoinsv.bitcoinjsv:bitcoinj-base:1.0.2"
    implementation(project(":tools"))
    implementation(project(":net"))
    implementation(project(":store"))
    implementation(project(":store-keyValue"))
    implementation(project(":store-levelDB"))

    implementation "ch.qos.logback:logback-classic:1.2.3"

    // Testing
    testImplementation(project(":tools"))
    testImplementation 'org.codehaus.groovy:groovy'
    testImplementation "org.spockframework:spock-core:1.2-groovy-2.5"
    testImplementation "ch.qos.logback:logback-classic:1.2.3"
    testImplementation 'com.madgag.spongycastle:core:1.51.0.0'
    testImplementation 'org.iq80.leveldb:leveldb:0.12'
}

bootJar {
    enabled = false
}

jar {
    enabled = true
}


/****************************************
 * Packaging a FAT Jar with jCLServer
 ****************************************/
task packageJclServer(type: Jar) {
    manifest {
        attributes 'Main-Class': 'com.nchain.jcl.integration.JCLServer'
    }
    archiveFileName = 'jclServer.jar'
    from(configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude 'META-INF/*.SF', 'META-INF/*.RSA'
    }
    with jar
}

/****************************************
 * Publish the JAR
 ****************************************/
publishing {
    publications {
        mavenJava(MavenPublication) {

            artifactId = "$jclArtifactName"
            groupId = "$jclGroup"
            version = "$jclVersion"

            from components.java

            pom {
                name = "$jclArtifactName"
                description = 'Java implementation of Bitcoin components'
                url = 'https://bitbucket.org/nchteamnch/java-component-library'

                licenses {
                    license {
                        name = ''
                        url = ''
                    }
                }
                developers {
                    developer {
                        name = 'Ivan Fernandez'
                        email = 'i.fernandez@nchain.com'
                    }
                    developer {
                        name = 'Michael Fletcher'
                        email = 'm.fletcher@nchain.com'
                    }
                }
                scm {
                    url = 'https://bitbucket.org/nchteamnch/java-component-library'
                }
            }
        }
    }

    repositories {
        maven {
            allowInsecureProtocol = true
            credentials {
                username = "$nexusUser"
                password = "$nexusPassword"
            }
            if(project.version.endsWith('-SNAPSHOT')) {
                url "$nexusSnapshotsUrl"
            } else {
                url "$nexusReleasesUrl"
            }
        }
    }
}